<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="fb_TippUpDown" Id="{a2a65827-617e-46c7-83ee-96a802903857}" SpecialFunc="None">
    <Declaration><![CDATA[(*======================================================================================================================================*)
(* fb_TippUpDown: Für z.B: Drehzahl oder Ähnliches, Counter Hoch runter mit min max begrenzung und Longpush für schnell Bedienung   *)
(*
Date          	| Version    	| created under        	| Author          	| Remark *)
(*======================================================================================================================================*)
(*01/08/2024	| 1.00      		| TC2		  		| N.Kersting       | first release *)
(*======================================================================================================================================*)
FUNCTION_BLOCK fb_TippUpDown
VAR_INPUT
	I_bTippUp		:BOOL;
	I_bTippDown		:BOOL;
	I_nMin			:LREAL;
	I_nMax			:LREAL;
	I_nTipp			:LREAL;
	I_tLongpush		:TIME	:= t#800ms;
	I_tIntervall		:TIME	:= t#30ms;
END_VAR
VAR_OUTPUT
	Q_nWert			:LREAL;
	Q_bMax			:BOOL;
	Q_bMin			:BOOL;
END_VAR
VAR
	tonLongpushVerz	:TON;
	iStep			:INT;
	bLongpush		:BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[tonLongpushVerz();

CASE iStep OF
(*==================================*)
0:(*	INIT	*)

	bLongpush := FALSE;
	tonLongpushVerz.IN	:= FALSE;

	IF	I_bTippUp
	THEN
		iStep := 100;
	END_IF

	IF	I_bTippDown
	THEN
		iStep := 200;
	END_IF

(*==================================*)
100:(*	Tipp Up	*)

	tonLongpushVerz.IN	:= FALSE;

	IF (Q_nWert +I_nTipp)
		 < I_nMax
	THEN
		Q_bMax := 	FALSE;
		Q_bMin := 	FALSE;
		Q_nWert :=	Q_nWert +I_nTipp;

		IF bLongpush
		THEN
			iStep := 		iStep +20;
		ELSE
			iStep := 		iStep +10;
		END_IF
	ELSE
		iStep := 		999;
		Q_nWert :=	I_nMax;
		Q_bMin := 	FALSE;
		Q_bMax := 	TRUE;
	END_IF

(*==================================*)
110:(*	Tipp Up	*)

	tonLongpushVerz.IN	:= I_bTippUp;
	tonLongpushVerz.PT	:= I_tLongpush;

	IF tonLongpushVerz.Q
	THEN
		tonLongpushVerz.IN	:= FALSE;
		iStep := 		iStep +10;
	ELSIF
		NOT I_bTippUp
	THEN
		iStep := 0;
	END_IF

(*==================================*)
120:(*	Tipp Up Longpush	*)

	bLongpush := TRUE;

	tonLongpushVerz.IN	:= I_bTippUp;
	tonLongpushVerz.PT	:= I_tIntervall;

	IF tonLongpushVerz.Q
	THEN
		iStep := 		iStep -20;
	ELSIF
		NOT I_bTippUp
	THEN
		iStep := 0;
	END_IF

(*==================================*)
200:(*	Tipp Down	*)

	tonLongpushVerz.IN	:= FALSE;

	IF (Q_nWert -I_nTipp)
		 > I_nMin
	THEN
		Q_bMax := 	FALSE;
		Q_bMin := 	FALSE;
		Q_nWert :=	Q_nWert -I_nTipp;

		IF bLongpush
		THEN
			iStep := 		iStep +20;
		ELSE
			iStep := 		iStep +10;
		END_IF
	ELSE
		iStep := 		999;
		Q_nWert :=	I_nMin;
		Q_bMax := 	FALSE;
		Q_bMin := 	TRUE;
	END_IF

(*==================================*)
210:(*	Tipp Down	*)

	tonLongpushVerz.IN	:= I_bTippDown;
	tonLongpushVerz.PT	:= I_tLongpush;

	IF tonLongpushVerz.Q
	THEN
		tonLongpushVerz.IN	:= FALSE;
		iStep := 		iStep+10;
	ELSIF
		NOT I_bTippDown
	THEN
		iStep := 0;
	END_IF

(*==================================*)
220:(*	Tipp Up Longpush	*)

	bLongpush := TRUE;

	tonLongpushVerz.IN	:= I_bTippDown;
	tonLongpushVerz.PT	:= I_tIntervall;

	IF tonLongpushVerz.Q
	THEN
		iStep := 		iStep -20;
	ELSIF
		NOT I_bTippDown
	THEN
		iStep := 0;
	END_IF

(*==================================*)
999:(*	LIMIT	*)

	IF NOT	I_bTippUp
		AND NOT I_bTippDown
	THEN
		iStep := 0;
	END_IF
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="fb_TippUpDown">
      <LineId Id="27" Count="143" />
    </LineIds>
  </POU>
</TcPlcObject>